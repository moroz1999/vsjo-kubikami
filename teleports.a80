                        module	teleports
                        struct	teleport
room_x                      byte
room_y                      byte
room_pos_x                  byte
room_pos_y                  byte
dest_room_x                 byte
dest_room_y                 byte
dest_room_pos_x             byte
dest_room_pos_y             byte
                        ends

teleport_color1             equ	%01101100
teleport_color2             equ	%00000100
teleport_color              db	teleport_color1,teleport_color2
check_hero
                            ld	hl,rooms.room_teleports_buf
check_hero_next
                            ld	a,(hl)
                ; check if there are no teleports
                            cp	rooms.rooms_end_marker
                            jr	z,check_hero_no_teleport
                ; read teleport address to ix
                            ld	ixl,a
                            inc	hl

                            ld	a,(hl)
                            ld	ixh,a
                            inc	hl
                            push	hl
                ; ix now has teleport data start

                            ld	a,(ix+teleport.room_pos_x)
                            ld	d,a

                            ld	a,(ix+teleport.room_pos_y)
                            ld	e,a

                            ld	a,(hero.hero_x)
                            sub	d
                            jr	nz,check_hero_skip

                            ld	a,(hero.hero_y)
                            sub	e
                            jr	nz,check_hero_skip

                            ld	a,(just_teleported)
                            or	a
                            jr	nz,check_hero_exit

                            ld	a,(ix+teleport.dest_room_pos_x)
                            ld	(hero.hero_x),a

                            ld	a,(ix+teleport.dest_room_pos_y)
                            ld	(hero.hero_y),a

                            ld	a,(ix+teleport.dest_room_x)
                            ld	(rooms.room_number_x),a

                            ld	a,(ix+teleport.dest_room_y)
                            ld	(rooms.room_number_y),a

                            call	rooms.init_current_room

                            jr	check_hero_teleported
check_hero_skip
                            pop	hl
                            jr	check_hero_next
check_hero_no_teleport
                            xor	a
                            ld	(just_teleported),a

                            ret
check_hero_teleported
                            ld	a,1
                            ld	(just_teleported),a
check_hero_exit
                            pop	hl
                            ret
just_teleported             db	0

draw_current
                            ld	hl,rooms.room_teleports_buf
draw_next
                            ld	a,(hl)
                ; check if there are no teleports
                            cp	rooms.rooms_end_marker
                            ret	z
                            ld	a,(hl)
                ; read teleport address to ix
                            ld	ixl,a
                            inc	hl

                            ld	a,(hl)
                            ld	ixh,a
                            inc	hl
                            push	hl
                ; ix now has teleport data start

                            ld	a,(ix+teleport.room_pos_x)
                            ld	d,a

                            ld	a,(ix+teleport.room_pos_y)
                            ld	e,a


                            push	ix
                            push	de
                            push	bc

                            ld	hl,teleport_color
                            call	draw.draw_gigapoint

                            pop	bc
                            pop	de
                            pop	ix

                            pop	hl
                            jr	draw_next
                            ret

amount                      equ	(teleports_table_end-teleports_table)/2

teleports_table
                            dw	teleport_0_1
                            dw	teleport_0_3
                            dw	teleport_1_2_1
                            dw	teleport_1_2_2
                            dw	teleport_3_2
                            dw	teleport_5_2
teleports_table_end

teleport_0_1
                            teleport	{
                            0,  //room_x
                            1,  //room_y
                            17, //room_pos_x
                            9, //room_pos_y
                            1,  //dest_room_x
                            2,  //dest_room_y
                            2, //dest_room_pos_x
                            9  //dest_room_pos_y
                            }
teleport_0_3
                            teleport	{
                            0,  //room_x
                            3,  //room_y
                            30, //room_pos_x
                            19, //room_pos_y
                            3,  //dest_room_x
                            2,  //dest_room_y
                            17, //dest_room_pos_x
                            11  //dest_room_pos_y
                            }

teleport_1_2_1
                            teleport	{
                            1,  //room_x
                            2,  //room_y
                            2, //room_pos_x
                            9, //room_pos_y
                            0,  //dest_room_x
                            1,  //dest_room_y
                            17, //dest_room_pos_x
                            9  //dest_room_pos_y
                            }
teleport_1_2_2
                            teleport	{
                            1,  //room_x
                            2,  //room_y
                            29, //room_pos_x
                            9, //room_pos_y
                            5,  //dest_room_x
                            2,  //dest_room_y
                            23, //dest_room_pos_x
                            17  //dest_room_pos_y
                            }
teleport_3_2
                            teleport	{
                            3,  //room_x
                            2,  //room_y
                            17, //room_pos_x
                            11, //room_pos_y
                            0,  //dest_room_x
                            3,  //dest_room_y
                            30, //dest_room_pos_x
                            19  //dest_room_pos_y
                            }
teleport_5_2
                            teleport	{
                            5,  //room_x
                            2,  //room_y
                            23, //room_pos_x
                            17, //room_pos_y
                            1,  //dest_room_x
                            2,  //dest_room_y
                            29, //dest_room_pos_x
                            9  //dest_room_pos_y
                            }
                        endmodule