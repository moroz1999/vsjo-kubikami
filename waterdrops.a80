                        module	waterdrops
                        struct	waterdrop
room_x                      byte
room_y                      byte
room_start_y                byte
room_pos_x                  byte
room_pos_y                  byte
delay                       byte
timer                       byte
state                       byte
                        ends
state_drop                  equ	1
state_delay                 equ	0
amount                      equ	(waterdrops_table_end-waterdrops_table)/2
                            display	"draw_room_waterdrops ",draw_room_waterdrops
draw_room_waterdrops
                            ld	hl,rooms.room_waterdrops_buf
draw_room_wds_next
                            ld	a,(hl)

                            cp	rooms.rooms_end_marker
                            ret	z

                            inc	hl
                            ld	ixl,a

                            ld	a,(hl)
                            inc	hl
                            ld	ixh,a

                            ld	a,(ix+waterdrop.room_pos_x)
                            ld	d,a


                            ld	a,(ix+waterdrop.room_pos_y)
                            ld	e,a

                            push	hl
                            call	screen_utils.get_attrs_address
                            ld	a,%01001001
                            ld	(hl),a
                            pop	hl
                            jr	draw_room_wds_next

                            ret

move_waterdrops
                            ld	hl,rooms.room_waterdrops_buf
move_next
                            ld	a,(hl)

                            cp	rooms.rooms_end_marker
                            ret	z

                            inc	hl
                            ld	ixl,a

                            ld	a,(hl)
                            inc	hl
                            push	hl

                            ld	ixh,a
                            ld	a,(ix+waterdrop.state)
                            cp	state_drop
                            jr	z,move_drop
                            ld	a,(ix+waterdrop.timer)
                            dec	a
                            ld	(ix+waterdrop.timer),a
                            or	a
                            jr	nz,move_skip

                            ld	a,state_drop
                            ld	(ix+waterdrop.state),a
move_drop
                            ld	a,(ix+waterdrop.room_pos_x)
                            ld	d,a

                            ld	a,(ix+waterdrop.room_pos_y)
                            ld	e,a
                            push	de
                            call	draw.add_to_restore
                            pop	de

                            inc	e
                            push	de
                            call	objects.check_coords
                            pop	de
                            cp	1
                            jr	nz,move_restore_source
                            ld	a,e
                            ld	(ix+waterdrop.room_pos_y),a
move_skip
                            pop	hl
                            jr	move_next


move_restore_source
                            ld	a,state_delay
                            ld	(ix+waterdrop.state),a
                            ld	a,(ix+waterdrop.delay)
                            ld	(ix+waterdrop.timer),a

                            ld	a,(ix+waterdrop.room_start_y)
                            ld	(ix+waterdrop.room_pos_y),a
                            pop	hl
                            jr	move_next

waterdrops_table
                            dw	waterdrop_2_1_1
                            dw	waterdrop_2_1_2
                            dw	waterdrop_2_1_3
waterdrops_table_end
waterdrop_2_1_1
                            waterdrop	{
                            2,  //room_x
                            1,  //room_y
                            1,  //room_start_y
                            5,  //room_pos_x
                            1   //room_pos_y
                            40  //delay
                            30  //timer
                            0   //state_delay
                            }
waterdrop_2_1_2
                            waterdrop	{
                            2,  //room_x
                            1,  //room_y
                            2,  //room_start_y
                            12, //room_pos_x
                            2,   //room_pos_y
                            20 //delay
                            70  //timer
                            0   //state_delay
                            }

waterdrop_2_1_3
                            waterdrop	{
                            2,  //room_x
                            1,  //room_y
                            1,  //room_start_y
                            20, //room_pos_x
                            1   //room_pos_y
                            60 //delay
                            80  //timer
                            0   //state_delay
                            }
                        endmodule