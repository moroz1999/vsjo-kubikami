    module  game_process
loop
                    ld  a,1
                    call    draw.border
                    call    hero.switch_colors
                    call    hero.save_restore
                    call    hero.hero_gravity
                    call    hero.hero_move
room_logic_pointer  equ $+1
                    call    logic.empty
                    call    draw.draw_state
                    xor a
                    call    draw.border
                    ld  b,1
                    call    wait
                    jr  loop
keyboard
                    ld  a,#fb
                    in  a,#fe

                    bit 0,a			;q
                    jp  z,keyboard_up

                    ld  a,#fd
                    in  a,#fe

                    bit 0,a			;a
                    call    z,hero.move_hero_down

                    ld  a,#df
                    in  a,#fe

                    bit 0,a			;p
                    call    z,hero.move_hero_right


                    ld  a,#df
                    in  a,#fe

                    bit 1,a			;o
                    call    z,hero.move_hero_left

                    ld  a,#7f
                    in  a,#fe

                    bit 0,a			;space
                    call    z,hero.item_pressed

                    ret
keyboard_up
                    ld  a,#df
                    in  a,#fe

                    bit 0,a			;p
                    jp  z,hero.hero_jump_right

                    ld  a,#df
                    in  a,#fe

                    bit 1,a			;o
                    jp  z,hero.hero_jump_left

                    jp  hero.hero_jump_top
wait
                    ei
                    halt
                    djnz    wait
                    ret
endgame
                    call    draw.clear_screen
                    di
                    halt
    endmodule