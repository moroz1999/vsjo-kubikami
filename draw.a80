    module  draw
border_color        equ 0
restore             db  255
restore_x           db  255
restore_y           db  255
border
                    out #fe,a
                    ret
draw_panel
                    ld  d,0
                    ld  e,rooms.room_h
                    call    screen_utils.get_attrs_address
                    push    hl
                    pop de
                    inc de
                    ld  a,%01000111
                    ld  (hl),a
                    ld  bc,rooms.room_w*2
                    ldir
                    ret
draw_pattern
                    ld  hl,screen_utils.screen
                    ld  b,rooms.room_h*8/2
draw_pattern_2rows
                    push    bc
                    push    hl
                    push    hl
                    pop de
                    inc de

                    ld  a,%01010101
                    ld  bc,screen_utils.screen_w-1
                    ld  (hl),a
                    ldir

                    pop hl
                    call    down_hl
                    push    hl
                    push    hl
                    pop de
                    inc de

                    ld  a,%10101010
                    ld  (hl),a
                    ld  bc,screen_utils.screen_w-1
                    ldir
                    pop hl
                    call    down_hl
                    pop bc
                    djnz    draw_pattern_2rows
                    ret
draw_hero
                    ld  a,(hero.hero_x)
                    ld  d,a
                    ld  a,(hero.hero_y)
                    ld  e,a
                    call    screen_utils.get_attrs_address
                    ld  a,(hero.hero_color)
                    ld  (hl),a
                    ret
draw_state
                    call    restore_hero
                    call    elevators.draw_current
                    call    draw_room_items
                    call    teleports.draw_current
                    call    draw_hero


                    ret
                    display "restore_rectangle ",restore_rectangle
restore_rectangle
                ; input:
                ; d - x
                ; e - y
                ; b - width
                ; c - height
                    push    bc

                    push    de
                    call    rooms.get_current_room_pixel_address
                    pop de
                    push    hl
                    call    screen_utils.get_attrs_address
                    ex  hl,de
                ; de now has screen address                    
                    pop hl
                ; hl now has room pixel address
                    pop bc
restore_row
                    push    de
                    push    hl
                    push    bc
                ; bc now has restored w/h
restore_pixel
                    ld  a,(hl)
                    ld  (de),a
                    inc hl
                    inc de
                    djnz    restore_pixel

                    pop bc
                    pop hl
                    ld  de,rooms.room_w
                    add hl,de

                    pop de
                    push    hl
                    ld  hl,rooms.room_w
                    add hl,de
                    ex  hl,de
                    pop hl

                    dec c
                    jr  nz,restore_row
                    ret
restore_hero
                    ld  a,(restore)
                    cp  255
                    ret z
                    ld  a,(restore_x)

                    ld  d,a
                    ld  a,(restore_y)
                    ld  e,a
                    call    screen_utils.get_texture_address
                    push    hl
                    call    rooms.get_current_room_address
                    ex  hl,de
                    pop hl
                    push    hl
                    add hl,de
                    ld  a,(hl)
                    pop hl
                    ld  de,screen_utils.attrs
                    add hl,de
                    ld  (hl),a

                    ld  a,255
                    ld  (restore),a

                    ret
clear_screen
                    ld  hl,screen_utils.attrs
                    ld  de,screen_utils.attrs+1
                    ld  bc,screen_utils.attrs_size-1
                    ldir
                    ret
draw_room_items
                    ld  hl,rooms.room_items_buffer
draw_room_items_next
                    ld  a,(hl)
                    inc hl

                    cp  rooms.rooms_end_marker
                    ret z


                    ld  a,(hl)
                    inc hl
                    ld  d,a

                    ld  a,(hl)
                    inc hl
                    ld  e,a
                    push    hl
                    call    screen_utils.get_attrs_address
                    ld  a,%01100000
                    ld  (hl),a
                    pop hl
                    jr  draw_room_items_next
draw_room_sprite
        ;   input
        ;   hl - sprite address
        ;   de - room address        
        ;   b - sprite width
        ;   c - sprite height
        ;
        ;   output
        ;   hl - sprite address end
draw_room_sprite_row
                    push    de
                    push    bc
draw_room_sprite_byte
                    ld  a,(hl)
                    inc hl
                    ld  (de),a
                    inc de
                    djnz    draw_room_sprite_byte
                    pop bc
                    pop de

                    push    hl
                    ld  hl,rooms.room_w
                    add hl,de
                    ex  hl,de
                    pop hl

                    dec c
                    jr  nz,draw_room_sprite_row
                    ret
    ifused  down_hl
down_hl
                    inc h
                    ld  a,h
                    and 7
                    ret nz
                    ld  a,l
                    sub -32
                    ld  l,a
                    sbc a,a
                    and #f8
                    add a,h
                    ld  h,a
                    ret
    endif
    endmodule