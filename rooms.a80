    module  rooms
room_number_x       db  2
room_number_y       db  0
room_amount_x       equ 6
room_amount_y       equ 4
room_w              equ 32
room_h              equ 24
room_size           equ room_w*room_h
room_bg_color1      equ 0
room_bg_color2      equ %01000001
init_current_room
                    call    logic.change_logic_pointer
                    call    get_room_address
                    ld  de,screen_utils.attrs
                    ld  bc,room_size
                    ldir
                    ret
get_room_address
                ; output:
                ; hl - current room address 
                    ld  a,(room_number_y)
                    or  a
                    jr  z,get_room_address_skip_add

                    ld  b,a
                    xor a
get_room_address_y
                    add room_amount_x
                    djnz    get_room_address_y
get_room_address_skip_add
                    ld  b,a
                    ld  a,(room_number_x)
                    add b
                    add a
                    ; a now contains shift to room pointer

                    ld  l,a
                    ld  h,0
                    ld  de,room_pointers
                    add hl,de
                    ex  hl,de
                    ld  a,(de)
                    ld  l,a
                    inc de
                    ld  a,(de)
                    ld  h,a
                    ret


get_room_pixel_address
                ; input:
                ; de - coordinates
                ; output:
                ; hl - address in memory
                    call    screen_utils.get_texture_address
                    push    hl
                    call    get_room_address
                    ex  hl,de
                    pop hl
                    add hl,de
                    ret
get_room_color
                ; input:
                ; de - coordinates
                ; output:
                ; a - color of pixel
                    call    get_room_pixel_address
                    ld  a,(hl)
                    ret
set_left_room
                    ld  a,(room_number_x)
                    dec a
                    ld  (room_number_x),a
                    call    init_current_room
                    ret
set_right_room
                    ld  a,(room_number_x)
                    inc a
                    ld  (room_number_x),a
                    call    init_current_room
                    ret
set_bottom_room
                    ld  a,(room_number_y)
                    inc a
                    ld  (room_number_y),a
                    call    init_current_room
                    ret
set_top_room
                    ld  a,(room_number_y)
                    dec a
                    ld  (room_number_y),a
                    call    init_current_room
                    ret
room_pointers
                    dw  0
                    dw  room_1_0
                    dw  room_2_0
                    dw  room_3_0
                    dw  0
                    dw  0

                    dw  room_0_1
                    dw  room_1_1
                    dw  room_2_1
                    dw  room_3_1
                    dw  room_4_1
                    dw  room_5_1

                    dw  room_0_2
                    dw  0
                    dw  room_2_2
                    dw  0
                    dw  0
                    dw  0

                    dw  0
                    dw  room_1_3
                    dw  room_2_3
                    dw  room_3_3
                    dw  0
                    dw  0
    endmodule