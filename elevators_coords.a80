                        module  elevators_coords
coords
                            ld  hl,rooms.room_elevators_buf
coords_next
                            ld  a,(hl)
                ; check if there are no elevators
                            cp  rooms.elev_end_marker
                            ret z

                            ld  a,(hl)
                ; read elevator address to ix
                            ld  xl,a
                            inc hl

                            ld  a,(hl)
                            ld  xh,a
                            inc hl
                            push    hl
                ; ix now has elevator data start

                ; calculate global elevator's coordinate

                ; y = elevator_room_y * each_room_height + elevator_room_base_coord_y - elevator_current_height

                            ld  a,(ix+elevators.elevator.room_y)

                            ld  h,a
                            ld  e,rooms.room_h

                            call    math.mul8b

                            ld  a,l

                            add (ix+elevators.elevator.room_pos_y)
                            sub (ix+elevators.elevator.cur_height)
                            ld  (ix+elevators.elevator.y),a
                            
                            pop hl
                ; check if elevator is below, under or in current room
                            ld  b,a
                ; b now has a global y coordinate of elevator, now compare it to current room's top/bottom coordinates

                            ld  a,(rooms.room_bottom_y)
                            cp  b
                            jr  c,save_below              ; elevator's y > room's bottom y, elevator is below, skip drawing

                            ld  a,(rooms.room_top_y)
                            inc b
                            cp  b
                            jr  nc,save_above              ; elevator's y < rooms' top y, elevator is above room, draw leg only
                            ld  (ix+elevators.elevator.in_room),elevators.room_inside
                            jr  coords_next
save_above
                            ld  (ix+elevators.elevator.in_room),elevators.room_above
                            jr  coords_next
save_below
                            ld  (ix+elevators.elevator.in_room),elevators.room_below
                            jr  coords_next
                        endmodule